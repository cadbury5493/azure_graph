index=ssc_inspec_event_idx bunit=awscentral_qualys
| stats 
    count AS total_controls,
    count(eval(Status="Passed")) AS passed_controls,
    count(eval(Status="Failed")) AS failed_controls,
    count(eval(Status="Skipped")) AS skipped_controls
    BY "Host IP" Technology

| eval passed_skipped_controls = passed_controls + skipped_controls

| eval compliance_with_waivers = round(
    (passed_skipped_controls / total_controls) * 100,
    2
)

| eval compliance_without_waivers = round(
    (passed_controls / total_controls) * 100,
    2
)

| eval host_status_with_waivers = if(compliance_with_waivers >= 95, "Passing", "Failing")
| eval host_status_without_waivers = if(compliance_without_waivers >= 95, "Passing", "Failing")

| stats
    dc("Host IP") AS hosts_scanned

    count(eval(host_status_with_waivers="Passing")) AS passing_hosts_with_waivers
    count(eval(host_status_with_waivers="Failing")) AS failing_hosts_with_waivers

    count(eval(host_status_without_waivers="Passing")) AS passing_hosts_without_waivers
    count(eval(host_status_without_waivers="Failing")) AS failing_hosts_without_waivers

    avg(compliance_with_waivers) AS compliance_pct_with_waivers
    avg(compliance_without_waivers) AS compliance_pct_without_waivers

    BY Technology

| eval compliance_pct_with_waivers = round(compliance_pct_with_waivers, 2)
| eval compliance_pct_without_waivers = round(compliance_pct_without_waivers, 2)

| rename Technology AS platform

| table
    platform
    hosts_scanned
    passing_hosts_with_waivers
    failing_hosts_with_waivers
    passing_hosts_without_waivers
    failing_hosts_without_waivers
    compliance_pct_with_waivers
    compliance_pct_without_waivers


----
index=ssc_inspec_event_idx bunit=awscentral_qualys
| stats 
    count AS total_controls,
    count(eval(Status="Passed")) AS passed_controls,
    count(eval(Status="Failed")) AS failed_controls,
    count(eval(Status="Skipped")) AS skipped_controls
    BY "Host IP" Technology "DNS Hostname"

| eval passed_skipped_controls = passed_controls + skipped_controls

| eval compliance_percentage = round(
    (passed_skipped_controls / total_controls) * 100,
    2
)

| eval compliance_percentage_considering_skipped_as_failed = round(
    (passed_controls / total_controls) * 100,
    2
)

| eval is_compliant_95 = if(compliance_percentage > 95, "true", "false")

| rename Technology AS platform "DNS Hostname" AS host_name

| table 
    "Host IP"
    host_name
    platform
    total_controls
    passed_skipped_controls
    failed_controls
    compliance_percentage
    passed_controls
    skipped_controls
    compliance_percentage_considering_skipped_as_failed
    is_compliant_95
